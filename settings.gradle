import java.io.FileInputStream
import java.util.*

pluginManagement.repositories {
	maven {
		name = "Architectury"
		url = "https://maven.architectury.dev/"
	}
	maven {
		name = "Fabric"
		url = "https://maven.fabricmc.net/"
	}
	maven {
		name = "Forge"
		url = "https://maven.minecraftforge.net/"
	}
	maven {
		name = "NeoForge"
		url = "https://maven.neoforged.net/releases/"
	}
    gradlePluginPortal()
}

/**
 * Borrowed from Distant Horizons
 */
def loadProperties() {
	String defaultVersion = "1.21"

	List<String> availableVersions = fileTree("versionProperties").files.name
	for (int i = 0; i < availableVersions.size(); i++) {
		availableVersions[i] = availableVersions[i].replaceAll("\\.properties", "")
	}

	availableVersions.sort()
	println "Avalible Minecraft versions: ${availableVersions}"

	String selectedVersion = ""
	int versionIndex = -1
	if (hasProperty("minecraftVersion")) {
		selectedVersion = minecraftVersion
		versionIndex = availableVersions.indexOf(minecraftVersion)
	}
	if (versionIndex == -1) {
		println "No 'minecraftVersion' set or the set 'minecraftVersion' is invalid! Defaulting to ${defaultVersion}."
		selectedVersion = defaultVersion
		versionIndex = availableVersions.indexOf(defaultVersion)
		assert versionIndex != -1
	}

	println "Loading properties file at " + selectedVersion + ".properties"
	var properties = new Properties()
	properties.load(new FileInputStream("${rootDir}/versionProperties/${selectedVersion}.properties"))

	properties.each { prop ->
		gradle.ext.set(prop.key, prop.value)
	}
	gradle.ext.availableVersions = availableVersions
	gradle.ext.versionIndex = versionIndex
}
loadProperties()

rootProject.name = "PandaMods Template"

include("common")
for (loader in ((String) gradle.supportedModLoaders).split(",")) {
	def loaderName = loader.strip()
	println "Adding loader " + loaderName
	include(loaderName)
}
